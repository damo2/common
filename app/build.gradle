import com.android.build.gradle.api.ApplicationVariant
import com.android.builder.core.DefaultProductFlavor
import org.gradle.api.internal.DefaultDomainObjectSet

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
//方法数
apply plugin: 'com.getkeepsafe.dexcount'
//流量监控
apply plugin: 'android-aspectjx'

Properties props = new Properties()
//加载local.properties 读取签名文件信息
props.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.weiyao.zuzuapp"
        customAppName(applicationVariants, defaultConfig)
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "armeabi-v7a"
        }

        manifestPlaceholders = [
                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY : "xxxxxxxxxxxxxxxxxxxxxx", //JPush 上注册的包名对应的 Appkey.
                JPUSH_CHANNEL: "developer-default", //暂时填写默认值即可.
        ]
    }
    signingConfigs {
        release {
            try {
                keyAlias props['RELEASE_KEY_ALIAS']
                keyPassword props['RELEASE_KEY_PASSWORD']
                storeFile file(props['RELEASE_STORE_FILE'])
                storePassword props['RELEASE_STORE_PASSWORD']
            }
            catch (e) {
                throw new InvalidUserDataException("请在 local.properties 添加签名配置 RELEASE_KEY_ALIAS、RELEASE_KEY_PASSWORD、RELEASE_STORE_FILE、RELEASE_STORE_PASSWORD。\n$e")
            }
        }
    }
    buildTypes {
        debug {
//            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            signingConfig signingConfigs.release
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions("ip")
    productFlavors {
        //IP地址
        Online {
            dimension "ip"
            resValue "string", "AppName", "达摩"
        }
        Moni {
            dimension "ip"
            resValue "string", "AppName", "达摩-模拟"
        }
        Offline {
            dimension "ip"
            resValue "string", "AppName", "达摩-测试"
        }
    }
    dexOptions {
        preDexLibraries true
        maxProcessCount 8
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def customAppName(DefaultDomainObjectSet<ApplicationVariant> applicationVariants, DefaultProductFlavor defaultConfig) {
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "damo-${variant.buildType.name}-${defaultConfig.versionName}-${new Date().format("yyyyMMdd")}"
            variant.productFlavors.forEach() { it ->
                outputFileName += "-" + it.name
            }
            outputFileName += ".apk"
            outputFileName.toLowerCase()
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0-alpha04'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha04'
    implementation 'com.google.android:flexbox:1.0.0'
    //
    //kotlin工具
    implementation 'org.jetbrains.anko:anko:0.10.5'
    //    implementation "org.jetbrains.anko:anko-commons:0.10.0"
    //网络请求
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    //json
    implementation 'com.google.code.gson:gson:2.8.2'
    //图片加载
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    //Rxjava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    //权限
    implementation 'pub.devrel:easypermissions:1.0.0'
    implementation project(':libcache')
    implementation project(':common')
    implementation 'cn.jiguang.sdk:jpush:3.1.3'
    implementation 'cn.jiguang.sdk:jcore:1.2.1'
    implementation 'com.qmuiteam:qmui:1.2.0'
    //助手
    debugImplementation('com.didichuxing.doraemonkit:doraemonkit:1.1.6') {
        exclude group: 'androidx.swiperefreshlayout', module: 'swiperefreshlayout'
        exclude group: 'androidx.coordinatorlayout', module: 'coordinatorlayout'
        exclude group: 'androidx.asynclayoutinflater', module: 'asynclayoutinflater'
        exclude group: 'androidx.legacy', module: 'legacy-support-core-ui'
        exclude group: 'androidx.print', module: 'print'
        exclude group: 'androidx.recyclerview', module: 'recyclerview'
        exclude group: 'androidx.legacy', module: 'legacy-support-core-utils'
    }
    releaseImplementation 'com.didichuxing.doraemonkit:doraemonkit-no-op:1.1.6'
    //启动框架
    implementation 'com.alibaba.android:alpha:1.0.0.1@aar'
    //保存恢复数据
    kapt 'com.evernote:android-state-processor:1.4.1'
    implementation project(':libloginshared')

    // ViewModel和LiveData依赖
    implementation "android.arch.lifecycle:extensions:1.1.0"
    annotationProcessor "android.arch.lifecycle:compiler:1.1.0"

    //CacheWebView
    implementation 'ren.yale.android:cachewebviewlib:2.1.8'
}

allprojects {
    configurations.all {
        //版本冲突：强制使用某版本依赖
        resolutionStrategy.force 'androidx.print:print:1.0.0-alpha3'

        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                def requested = details.requested
                if (requested.group == 'com.android.support') {
                    if (!requested.name.startsWith("multidex")) {
                        details.useVersion '27.1.1'//27.1.1为当前版本号
                    }
                }
            }
        }
    }
}

//如果有用到kapt添加如下配置
kapt {
    useBuildCache = true
    javacOptions {
        option("-Xmaxerrs", 500)
    }
}
